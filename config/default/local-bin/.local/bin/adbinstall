#!/usr/bin/env zsh

local help

local name="${0:t}"
local usage=(
    'Usage:'
    "\t$name [-h|--help]"
)

local deps=(
    'adb'
    'fd'
    'fzf'
)

zparseopts -D -F -- \
    {h,-help}=help ||
    { print -l "\n${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)usage}" && exit 0 }

function check_deps() {
    [[ -n $deps ]] || { echo '[!] Incorrect env - check_deps' && exit 2 }

    local failed
    for dep in "${(@)deps}"; do
        type "$dep" &>/dev/null || { echo '[!] Missing -' "$dep" && failed=1 }
    done

    [[ -z $failed ]] || { exit 1 }
}

function main() {
    check_deps

    fd -t f -e apk -IH --max-depth 10 | \
    fzf \
        --multi \
        --preview 'stat -c %Y {} | xargs -I {} date -d @{}' \
        --preview-window "right:$(( $(tput cols) / 4 * 3 )):border-vertical" \
        --bind "ctrl-\\:change-preview-window($(( $(tput cols) / 5 ))|$(( $(tput cols) / 4 * 3 )))" | \
    xargs -I {} adb install -d -r {}
}

main
