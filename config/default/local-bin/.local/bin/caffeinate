#!/usr/bin/env zsh

local help

local description=(
    'Caffeinate wrapper using minutes'
    '\nUsed opts:'
    '\t-d=No display sleep'
    '\t-m=No disk sleep'
    '\t-i=No system idle\n'
)

local name="${0:t}"
local usage=(
    'Usage:'
    "\t$name [-h|--help]"
    "\t$name <minutes>"
)

zparseopts -D -F -- \
    {h,-help}=help ||
    { print -l "\n${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)description}" "${(@)usage}" && exit 0 }
[[ -n $1 && $1 =~ ^[0-9]+(\.[0-9]+)?$ ]] || { echo '[!] Absent or incorrect var' && exit 1 }

killall caffeinate &>/dev/null || true
( /usr/bin/caffeinate -dmi -t "$(printf %d $(( $1 * 60 )))" & disown ) &>/dev/null || true
