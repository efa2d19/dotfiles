#!/usr/bin/env zsh

local help

local name="${0:t}"
local usage=(
    'Usage:'
    "\t$name [-h|-help]"
    '\nUses $NICE_EXT of kv pair with : as separator to execute stuff'
)

local deps=(
    'procs'
)

zparseopts -D -F -- \
    {h,-help}=help ||
    { print -l "\n${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)usage}" && exit 0 }

local nice_arr

function check_deps() {
    [[ -n $deps ]] || { echo '[!] Incorrect env - check_deps' && exit 2 }

    local failed
    for dep in "${(@)deps}"; do
        type "$dep" &>/dev/null || { echo '[!] Missing -' "$dep" && failed=1 }
    done

    [[ -z $failed ]] || { exit 1 }
}

function get_extensions() {
    [[ $NICE_EXT ]] || {
        echo '[!] No extensions found'
        exit 1
    }

    echo '[?] Extensions found'

    read -rA ext_arr <<< "$NICE_EXT"

    for key val in "${(@kv)ext_arr}"; do
        [[ -n ${nice_arr[$key]+_} ]] || {
            nice_arr[$key]="$val"
            continue
        }

        nice_arr[$key]="${nice_arr[$key]}:${val}"
    done

    unset ext_arr
}

function acquire_root() {
    sudo echo &>/dev/null || { echo '[!] Incorrect credentials' && exit 1 }
}

function process() {
    for key val in "${(@kv)nice_arr}"; do
        (( $key >= 0 )) && { priority="+${key}" } || { priority="$key" }

        for item in "${(@s/:/)val}"; do
            (
                local item_pids=($(sudo procs $item --only pid --no-header | awk '{ print $1 }'))

                [[ -n $item_pids ]] || {
                    echo "[?] ${item} - empty"
                    continue
                }

                echo "[?] ${item} - processing..."
                sudo renice "$priority" -p "${(@)item_pids}"
            ) &
        done
    done

    wait
}

function main() {
    check_deps
    declare -A nice_arr
    get_extensions
    acquire_root
    process
}

main
