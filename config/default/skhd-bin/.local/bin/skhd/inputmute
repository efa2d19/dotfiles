#!/usr/bin/env zsh

local deps=(
    'SwitchAudioSource'
    'terminal-notifier'
)

function check_deps() {
    [[ -n $deps ]] || { echo '[!] Incorrect usage - check_deps' && exit 2 }

    local failed
    for dep in "${(@)deps}"; do
        type "$dep" &>/dev/null || { echo '[!] Missing -' "$dep" && failed=1 }
    done

    [[ -z $failed ]] || { exit 1 }
}

function main() {
    check_deps

    terminal-notifier \
        -title 'InputMute' \
        -message "$(SwitchAudioSource -m toggle -t input)"
}

local scoped_fd
local lock='/tmp/inputmute.lock'
exec {scoped_fd}>"$lock"

(
    lockf -st 0 "$scoped_fd" || { echo '[!] Locked -' "$lock" && exit 0 }
    trap 'exec {scoped_fd}>&- && unlink "$lock"' EXIT INT
    main
)
