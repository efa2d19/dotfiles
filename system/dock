#!/usr/bin/env zsh

###############################################################################
# Special thanks to                                                           #
# https://github.com/webpro/dotfiles/                                         #
###############################################################################

local help clear dock_items

local usage=(
    'Adds stuff from exported DOCK_EXT w/ CRNL as separator to Dock'
    'Plus additional ones if specified as argv'
    'Usage:'
    '\tdock [-h|--help]'
    '\tdock [-c|--clear] <vararg additional_items: String>'
)

zparseopts -D -F -- \
    {h,-help}=help \
    {c,-clear}=clear ||
    { print -l "${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)usage}" && exit 0 }

function get_ext() {
    [[ $DOCK_EXT ]] || { echo '[!] No extensions found' && exit 1 }

    dock_items=(${(f)DOCK_EXT})
    [[ ${#@} == 0 ]] || { dock_items+=("$@") }
}

function main() {
    # Clear all but finder
    [[ -z $clear ]] || dockutil --no-restart --remove all

    # Add custom ones
    for item in "${(@)dock_items}"; do
        dockutil --no-restart --add "$item"
    done

    # Restart dock
    killall Dock
}

get_ext $@
main
