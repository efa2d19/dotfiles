#!/usr/bin/env zsh

##################################################
# Special thanks to                              #
# https://github.com/webpro/dotfiles/            #
##################################################

local help clear

local description=(
    'Populates dock with apps from DOCK_EXT env with CRNL as separator'
    'Plus additional ones if specified as argv'
)

local name="${0:t}"
local usage=(
    'Usage:'
    "\t$name [-h|--help]"
    "\t$name [-c|--clear]"
    "\t$name <vararg additional_items>"
)

zparseopts -D -F -- \
    {h,-help}=help \
    {c,-clear}=clear ||
    { print -l "\n${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)description}" "${(@)usage}" && exit 0 }

function reset_dock() {
    dockutil --no-restart --remove all
}

function restart_dock() {
    killall Dock
}

function main() {
    local dock_items=()
    typeset -U dock_items

    dock_items=(${(f)DOCK_EXT})
    dock_items+=($@)

    [[ -z $clear ]] || reset_dock

    for item in "${(@)dock_items}"; do
        dockutil --no-restart --add "$item"
    done

    restart_dock
}

main $@
